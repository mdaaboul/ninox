/* ------------------------------------------
** A collection of global functions that deal with the realities of dates in Ninox
** -----------------------------------------*/

function IsDate(xDate : date) do
	if number(xDate) = 0 then false else true end
end;
function DateOnly(xDate : date) do
	date(year(xDate), month(xDate), day(xDate))
end;
function NDate(d : date) do
	number(DateOnly(d))
end;
function DateCompare(d1 : date,d2 : date,op : text) do
	switch op do
	case "<":
		NDate(d1) < NDate(d2)
	case ">":
		NDate(d1) > NDate(d2)
	case "<=":
		NDate(d1) <= NDate(d2)
	case ">=":
		NDate(d1) >= NDate(d2)
	default:
		NDate(d1) = NDate(d2)
	end
end;
function between(d : date,s : date,e : date) do
	if NDate(d) >= NDate(s) and NDate(d) <= NDate(e) then
		true
	else
		false
	end
end;
/* ---------------------------------------------
** Add W workdays to a date returning the workday that would result
** ---------------------------------------------*/
function WorkEndDate(S : date,W : number) do
	var X := 1;
	if W > 1 then
		while workdays(S, date(year(S), month(S), day(S) + X + 1)) <= W and X < 365 do 
			X := X + 1
		end
;
		if weekday(date(year(S), month(S), day(S) + X)) = 6 then
			date(year(S), month(S), day(S) + X - 2)
		else
			date(year(S), month(S), day(S) + X)
		end
	else
		S
	end
end;
